module org/sugarj/stdlib/StdTrans
imports libstrategolib

imports org/sugarj/transformations/GenerateSignatures
imports org/sugarj/transformations/LayoutSugar
imports org/sugarj/transformations/Renaming

signature constructors
  NextToplevelDeclaration : ToplevelDeclaration * Rest -> NextToplevelDeclaration

strategies
  compile-error = ?CompileError(_,_)
  compile-error(|msg) = !CompileError(msg,<id>)
  
  desugar = fail
  changed-tree(s) = ?t; s; not(equal(|t))
  
  unlocked-bottomup(s) = ?Locked(t) < id + all(unlocked-bottomup(s)); s
  traverse(s) = compile-error <+ unlocked-bottomup(try(s; traverse(s)))
  unlock = bottomup(try(?Locked(<id>)))

  desugar-main = internal-main
  internal-main = traverse(desugar); unlock
  main = io-wrap(internal-main)

  sugarj-analyze:
    (ast, path, project-path) -> (ast, errors, warnings, notes)
    with
      editor-init;
      try(analyze);
      errors   := <collect-all(constraint-error, conc)> ast;
      warnings := <collect-all(constraint-warning, conc)> ast;
      notes    := <collect-all(constraint-note, conc)> ast

    editor-init =
      // Ensure all dynamic rules are properly scoped
      try(dr-scope-all-end);
      dr-scope-all-start

    analyze = fail
    constraint-error = fail
    constraint-warning = fail
    constraint-note = fail

signature
  constructors
    Locked : a -> a
    CompileError : A * B -> C

signature
  constructors
    Some : a -> Option(a)
    None : Option(a)


signature
  constructors
    Cons : a * List(a) -> List(a)
    Nil  : List(a)
    Conc : List(a) * List(a) -> List(a)